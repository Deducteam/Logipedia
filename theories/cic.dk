
(;-----------------------------------------------------------------;)
(;----------------------  PUBLIC DEFINITIONS  ---------------------;)
(;-----------------------------------------------------------------;)


(;---------------------------  Sorts  -----------------------------;)

Sort : Type.
sinf : Sort.

(;----------------------  CTS Sorts definition  -------------------;)

Nat : Type.
z : Nat.
s : Nat -> Nat.
prop : Sort.
type : Nat -> Sort.

def succ : Sort -> Sort.
[] succ prop --> type z.
[i] succ (type i) --> type (s i).

(;---------------------  Types and terms  -------------------------;)

(; Type for encoded type codes ;)
Univ : s : Sort -> Type.

(; Type decoding function ;)
def Term : s : Sort -> a : Univ s -> Type.


(;---------------------------  Predicates  ------------------------;)

Bool : Type.
eps : Bool -> Type.
true : Bool.
def forall : s : Sort -> a : Univ s -> (Term s a -> Bool) -> Bool.
[] forall _ _ (x => true) --> true.
I : eps true.


(;---------------------  CTS Axiom definition  --------------------;)

def Axiom : Sort -> Sort -> Bool.
[   ] Axiom prop         (type z    ) --> true.
[   ] Axiom (type z    ) (type (s z)) --> true.
[i,j] Axiom (type (s i)) (type (s j)) --> Axiom (type i) (type j).


(;-----------------------  CTS Rule definition  -------------------;)

def Rule : Sort -> Sort -> Sort -> Bool.
[     ] Rule _            prop         prop         --> true.
[i    ] Rule prop         i            i            --> true.
[i    ] Rule (type i    ) (type z    ) (type i    ) --> true.
[j    ] Rule (type z    ) (type j    ) (type j    ) --> true.
[i,j,k] Rule (type (s i)) (type (s j)) (type (s k)) --> Rule (type i) (type j) (type k).


(;-------------------  CTS Cumulativity definition  ---------------;)

def Cumul : Sort -> Sort -> Bool.
[]    Cumul prop         _            --> true
[]    Cumul (type z)     (type _)     --> true
[i,j] Cumul (type (s i)) (type (s j)) -->
      Cumul (type i)     (type j).


(;--------------------  Term public constructors  -----------------;)

def univ : s : Sort -> s' : Sort ->
           p : eps (Axiom s s') -> Univ s'.

def prod : s1 : Sort -> s2 : Sort -> s3 : Sort ->
           p : eps (Rule s1 s2 s3) ->
           a : Univ s1 -> b : (Term s1 a -> Univ s2) -> Univ s3.

def SubType : s : Sort -> s' : Sort -> Univ s -> Univ s' -> Bool.

def cast : s : Sort -> s' : Sort ->
           a : Univ s -> b  : Univ s' ->
           p : eps (SubType s s' a b) ->
           Term s a -> Term s' b.


(;-----------------------------------------------------------------;)
(;---------------------  PRIVATE DEFINITIONS  ---------------------;)
(;-----------------------------------------------------------------;)

(;----------------------------  Axiom  ----------------------------;)

univ' (s : Sort) (s' : Sort) : Univ s'.

[s] Term _ (univ' s _) --> Univ s.

[s,s',p] univ s s' p --> univ' s s'.


(;----------------------------  Rule  -----------------------------;)

def prod' : s1 : Sort -> s2 : Sort -> s3 : Sort ->
            a : Univ s1 -> b : (Term s1 a -> Univ s2) -> Univ s3.

[s1, s2, a, b] Term _ (prod' s1 s2 _ a b) --> x : Term s1 a -> Term s2 (b x).

[s1, s2,s3,p] prod s1 s2 s3 p --> prod' s1 s2 s3.


(;-------------------------  Subtyping ----------------------------;)

[s1, s2]           SubType _  _  (univ' s1 _)       (univ' s2 _ )        --> Cumul s1 s2
[s1,s2,s2',a,b,b'] SubType _  _ (prod' s1 s2 _ a b) (prod' _ s2' _ a b') -->
                  forall s1 a (x => SubType s2 s2' (b x) (b' x)).
[a]          SubType _ _ a a --> true.

(;---------------------------  Casts  -----------------------------;)

def cast' : s : Sort -> s' : Sort ->
            a : Univ s -> b : Univ s' ->
            Term s a -> Term s' b.

[s, a] Term _ (cast' _ _ (univ' s _) _ a) --> Term s a.

[s1,s2,a,b,t] cast s1 s2 a b _ t --> cast' s1 s2 a b t.

(;---------------------  Canonicity rules -------------------------;)

[A,t] cast' _ _ A A t --> t.

[s, s', a, c, t]
  cast' _ s' _ c (cast' s _ a _ t) -->
  cast' s s' a c t.
(;
[s1,s2,s3]
 cast' _ _ _ (univ' s3 _) (univ' s1 s2) --> univ' s1 s3.
;)
[s1,s2,s3, a, b]
  prod' _ s2 s3 (cast' _ _ (univ' s1 _) (univ' _ _) a) b -->
  prod' s1 s2 s3 a b.

[s1, s2, s3, a, b]
  prod' s1 _  s3 a (x => cast' _ _ (univ' s2 _) (univ' _ _) (b x)) -->
  prod' s1 s2 s3 a (x => b x).

[s1,s2,s3,s4,a,b]
  cast' _ _ (univ' _ _) (univ' s4 _) (prod' s1 s2 s3 a b) --> prod' s1 s2 s4 a b.


(; This version encodes both contravariant and covariant product subtyping. ;)
[s1,s1',s2,s3,A,A',B,C,b]
  cast' _ _ (prod' s1 s2 _ A B) (prod' s1' s3 _ A' C) (x => b x) -->
  x : Term s1' A' => cast' s2 s3 (B (cast' s1' s1 A' A x)) (C x) (b (cast' s1' s1 A' A x)).


(; This version encodes only covariant product subtyping.
[s1,s2,s3,A,B,C,b]
  cast' _ _ (prod' s1 s2 _ A B) (prod' _ s3 _ A C) (x => b x) -->
  x : Term s1 A => cast' s2 s3 (B x) (C x) (b x).
;)
[s1,s1',s2,s3,A,A',B,C,b,a]
  cast' _ _ (prod' s1 s2 _ A B) (prod' s1' s3 _ A' C) b a -->
  cast' s2 s3 (B (cast' s1' s1 A' A a)) (C a) (b (cast' s1' s1 A' A a)).

[s1,s2,A,B,a] cast' _ s2 (cast' _ _ (univ' s1 _) _ A) B a --> cast' s1 s2 A B a.

[s1,s2,A,B,a] cast' s1 _ A (cast' _ _ (univ' s2 _) _ B) a --> cast' s1 s2 A B a.